buildscript {
    repositories {
        maven { url "https://cache-redirector.jetbrains.com/intellij-repository/snapshots" }
        maven { url "https://cache-redirector.jetbrains.com/maven-central" }
        maven { url "https://cache-redirector.jetbrains.com/intellij-plugin-service" }
    }
}

plugins {
    id "org.jetbrains.intellij" version "${gradle_plugin_version}"
    id "org.jetbrains.kotlin.jvm" version "${kotlin_version}"
    id "de.undercouch.download" version "${undercouch_version}"
    id "io.freefair.aspectj.post-compile-weaving" version "${aspectj_version}"
}

allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'java'

    repositories {
        mavenLocal()
        mavenCentral()

        // we use this repo to get libraries installed from building Utils libraries
        maven { url new File(rootProject.projectDir, '../../.repository').toURI() }

        maven { url 'https://cache-redirector.jetbrains.com/intellij-repository/snapshots' }
        maven { url 'https://cache-redirector.jetbrains.com/maven-central' }
    }

    sourceSets {
        main {
            java.srcDirs 'src'
            kotlin.srcDirs 'src'
            resources {
                srcDir 'resources'
                exclude 'bundle/**'
            }
        }
        test {
            java.srcDirs = [ 'test/java', 'Test/java' ]
            kotlin.srcDirs = [ 'test/kotlin', 'Test/java' ]
            resources.srcDirs = [ 'test/resources', 'Test/resources' ]
        }
    }
}

def mainProjects = [project(':'), project(':idea'), project(':rider')]

configure(mainProjects) {
    apply plugin: 'org.jetbrains.intellij'
    apply plugin: 'io.freefair.aspectj.post-compile-weaving'

    group 'com.microsoft.azuretools'

    compileJava {
        sourceCompatibility = javaVersion
        targetCompatibility = javaVersion
    }

    compileKotlin {
        kotlinOptions.jvmTarget = javaVersion
        kotlinOptions.freeCompilerArgs += '-Xopt-in=kotlin.RequiresOptIn'
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = javaVersion
    }

    dependencies {
        implementation "org.aspectj:aspectjtools:${aspectj_tools_version}"
        implementation "org.aspectj:aspectjrt:${aspectj_tools_version}"
        implementation 'com.microsoft.sqlserver:mssql-jdbc:6.4.0.jre8'
        implementation 'commons-io:commons-io:2.7'
        implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
        implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.10'
        implementation 'net.minidev:json-smart:2.3'
        implementation 'com.microsoft.azure:azure-client-runtime:1.7.5', { force = true }
        implementation 'com.microsoft.azure:azure-client-authentication:1.7.5', { force = true }
        aspect 'com.microsoft.azure:azure-toolkit-common-lib:0.3.0'
        implementation 'com.microsoft.azuretools:azuretools-core:3.50.0', {
            exclude group: "com.microsoft.azure", module: "azure-client-authentication"
            exclude group: "com.microsoft.azure", module: "azure-client-runtime"
            exclude group: "javax.xml.bind", module: "jaxb-api"
        }
        implementation 'com.microsoft.azuretools:azure-explorer-common:3.50.0', {
            exclude group: "com.microsoft.azure", module: "azure-client-authentication"
            exclude group: "com.microsoft.azure", module: "azure-client-runtime"
            exclude group: "javax.xml.bind", module: "jaxb-api"
        }
        implementation 'com.microsoft.azuretools:hdinsight-node-common:3.50.0', {
            exclude group: "com.microsoft.azure", module: "azure-client-authentication"
            exclude group: "com.microsoft.azure", module: "azure-client-runtime"
            exclude group: "javax.xml.bind", module: "jaxb-api"
        }
        implementation group: 'com.microsoft.azure', name: 'azure-auth-helper', version: '0.6.0'
        implementation 'com.microsoft.azure:azure-tools-common:0.10.0'
        implementation group: 'com.microsoft.azure.appplatform.v2020_07_01', name: 'azure-mgmt-appplatform', version: '1.0.0-beta'
        implementation group: 'org.dom4j', name: 'dom4j', version: '2.1.3', {
            exclude group: "javax.xml.stream", module: "stax-api"
            exclude group: "xpp3", module: "xpp3"
            exclude group: "pull-parser", module: "pull-parser"
            exclude group: "net.java.dev.msv", module: "xsdlib"
        }
        implementation group: 'jaxen', name: 'jaxen', version: '1.2.0'
        implementation group: 'com.microsoft.azure.appplatform.v2019_05_01_preview', name: 'azure-mgmt-appplatform', version: '1.0.0-beta-2'
        implementation group: 'commons-beanutils', name: 'commons-beanutils', version: '1.9.4'

        implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}"
        implementation "com.fasterxml.jackson.module:jackson-module-kotlin:${jackson_kotlin_version}"

        implementation group: 'com.jcraft', name: 'jsch', version: '0.1.55'
        implementation group: 'com.neovisionaries', name: 'nv-websocket-client', version: '2.9'
        implementation group: 'mysql', name: 'mysql-connector-java', version: '8.0.23'

        implementation group: 'org.apache.maven', name: 'apache-maven', version: '3.9.0'

        // https://mvnrepository.com/artifact/org.projectlombok/lombok
        compileOnly "org.projectlombok:lombok:${lombok_version}"
        annotationProcessor "org.projectlombok:lombok:${lombok_version}"

        testImplementation "org.assertj:assertj-core:${assertj_version}"
    }

    configurations {
        implementation.exclude module: 'slf4j-api'
        implementation.exclude module: 'log4j'
        implementation.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        implementation.exclude module: 'stax-api'
    }

    tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

    intellij {
        switch (build_common_code_with) {
            case 'rider':
                version = rider_version
                logger.lifecycle("COMMON_SDK=$rider_version")
                break
            case 'idea':
                version = intellij_version
                logger.lifecycle("COMMON_SDK=$intellij_version")
                break
            default:
                throw new Exception("Unsupported value for build_common_code_with: " + build_common_code_with)
        }

        plugins = ['terminal']

        updateSinceUntilBuild = Boolean.valueOf(updateVersionRange)
        downloadSources = Boolean.valueOf(sources)
    }

    runIde {
        jvmArgs '-Xmx2048m'
    }

    test {
        testLogging.showStandardStreams = true
        testLogging {
            events "passed", "skipped", "failed"
        }
        testLogging.exceptionFormat "full"
    }

    tasks.named("processTestResources") {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    }

    buildSearchableOptions.onlyIf { false }
}

defaultTasks 'buildPlugin'

wrapper {
    gradleVersion = '7.6'
    distributionType = Wrapper.DistributionType.ALL
    distributionUrl = "https://cache-redirector.jetbrains.com/services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}
