import org.jetbrains.intellij.tasks.PrepareSandboxTask

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output

        kotlin.srcDirs = ['test-integration']
        resources.srcDirs = ['test-integration/resources']
    }
}

dependencies {
    compile rootProject
    compile "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "org.java-websocket:Java-WebSocket:$web_socket_version"

    testImplementation rootProject
}

configurations {
    compile.exclude module:'stax-api'
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

ext.resharperPluginPath = new File(projectDir, 'ReSharper.Azure')

apply from: 'backend.gradle'

// Plugin version
if (!ext.has('BuildNumber'))
    ext.BuildNumber = buildNumber

version "$BuildNumber"
logger.lifecycle("version=$version")
println "##teamcity[buildNumber '$version']"

def extensionsFrom = 'resources/dotnet'

intellij {
    pluginName = rider_plugin_name
    version = rider_version

    // Workaround for https://youtrack.jetbrains.com/issue/IDEA-179607
    def extraPlugins = [ "rider-plugins-appender" ]
    plugins = ['DatabaseTools', 'CSS', 'JavaScriptLanguage', 'terminal', 'restClient'] + extraPlugins

    updateSinceUntilBuild = Boolean.valueOf(updateVersionRange)
    downloadSources = Boolean.valueOf(sources)
}

tasks.withType(PrepareSandboxTask).configureEach {
    dependsOn buildReSharperPlugin

    from(extensionsFrom, {
        into "$rider_plugin_name/dotnet"
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    })
    from(buildReSharperPlugin.outputs, {
        into "$rider_plugin_name/dotnet"
        duplicatesStrategy DuplicatesStrategy.INCLUDE
    })
}

buildSearchableOptions.onlyIf { false }

patchPluginXml {
    sinceBuild = patchPluginXmlSinceBuild
}

// Add Temporary workaround to call ':rider:prepareTestingSandbox' task - https://github.com/JetBrains/gradle-intellij-plugin/issues/743
// TODO: Remove this dependsOn task after 1.1.4 version of 'org.jetbrains.intellij' plugin is released
test {
    description = "Rider unit tests."
    dependsOn ':rider:buildPlugin', prepareTestingSandbox

    useTestNG()
}

// Add Temporary workaround to call ':rider:prepareTestingSandbox' task - https://github.com/JetBrains/gradle-intellij-plugin/issues/743
// TODO: Remove this dependsOn task after 1.1.4 version of 'org.jetbrains.intellij' plugin is released
task integrationTest(type: Test) {
    description = "Rider Azure integration tests based on Rider Test Framework."
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    dependsOn ':rider:buildPlugin', prepareTestingSandbox

    environment("LOCAL_ENV_RUN", rider_test_local_env_run)
    environment("NO_FS_ROOTS_ACCESS_CHECK", true)
    useTestNG()

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    jvmArgs '--illegal-access=warn'

    testLogging {
        showStandardStreams = true
    }

    doFirst {
        println "LOCAL_ENV_RUN = $rider_test_local_env_run"
    }
}

clean {
    dependsOn cleanBackend, ':rider:protocol:cleanProtocolModels'
}

jar {
    dependsOn ':rider:protocol:generateModel'
}
