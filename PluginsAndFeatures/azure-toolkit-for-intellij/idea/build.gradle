dependencies {
    compile rootProject

    testCompile 'junit:junit:4.12'
    testCompile 'info.cukes:cucumber-junit:1.2.5'
    testCompile 'info.cukes:cucumber-java:1.2.5'
    testCompile 'org.mockito:mockito-core:2.7.22'
    testCompile 'org.assertj:assertj-swing-junit:3.5.0'

    testCompile 'com.github.tomakehurst:wiremock:2.8.0'
    testCompile 'org.powermock:powermock-module-junit4:1.7.0RC4'
    testCompile 'org.powermock:powermock-api-mockito2:1.7.0RC4'
    testCompile 'javax.servlet:javax.servlet-api:3.1.0'
}

configurations {
    testCompile.exclude module:'htmlunit'
}

task cucumberPackJar(type: Jar) {
    appendix = 'pathing'

    doFirst {
        manifest {
            attributes "Class-Path": (configurations.testRuntime.files + configurations.compile.files).collect {
                it.toURL().toString().replaceFirst(/file:\/+/, '/')
            }.join(' ')
        }
    }
}

task cucumber() {
    dependsOn assemble, compileTestJava, cucumberPackJar
    doLast {
        javaexec {
            main = "cucumber.api.cli.Main"
            classpath = files(sourceSets.main.output,
                    sourceSets.test.output,
                    cucumberPackJar.archivePath)
            args = [
                    '--plugin', 'pretty',
                    '--glue', 'com.microsoft.azure.hdinsight.spark.common',
                    'test/resources']
        }
    }
}

test.dependsOn cucumber

intellij {
    pluginName = 'azure-toolkit-for-intellij'
    plugins = ['java', 'maven', dep_plugins]
    version = idea_version
}

buildSearchableOptions.enabled = false